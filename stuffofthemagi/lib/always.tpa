/* ============================================ *
 *                                              *
 *                 ALWAYS BLOCK                 *
 *                                              *
 * ============================================ */

// Concept borrowed from DavidW's Immutability and encapsulation in mod design ( https://github.com/Gibberlings3/immutable )

/* =================================================================== *
 *  This code only runs once, even if you install lots of components.  *
 * =================================================================== */

ACTION_IF !VARIABLE_IS_SET always_once_section BEGIN

	OUTER_SET always_once_section = 1
	SILENT

	/* -------------------------------------------------------------------------------- *
	 *    Determines which game is installed and if the game is an Enhanced Edition.    *
	 * -------------------------------------------------------------------------------- */
	OUTER_SET is_ee = (GAME_IS ~bg2ee eet~) ? 1 : 0					// EE game
	OUTER_SET is_tob = (GAME_IS ~bgt tob tutu tutu_totsc~) ? 1 : 0	// based on vanilla ToB engine
//	OUTER_SET is_tutu = (GAME_IS ~tutu tutu_totsc~) ? 1 : 0			// tutu
	OUTER_SET is_bg2ee = (GAME_IS "bg2ee") ? 1 : 0					// b2gee
	OUTER_SET is_eet = (GAME_IS "eet") ? 1 : 0						// eet


	/* ------------------------ *
	 *    Detects other mods    *
	 * ------------------------ */
	OUTER_SET is_1pp_helmet =  (MOD_IS_INSTALLED ~1pp.tp2~ ~208~) ? 1 : 0											// 1ppv4: Additional Helmet Animations (core)
	OUTER_SET is_1pp_staff  = ((MOD_IS_INSTALLED ~1pp.tp2~ ~207~) AND (MOD_IS_INSTALLED ~1pp.tp2~ ~400~)) ? 1 : 0	// 1ppv4: Wizards' Staves (core)
	OUTER_SET is_1pp_staff0 = ((MOD_IS_INSTALLED ~1pp.tp2~ ~204~) AND (is_1pp_staff = 0)) ? 1 : 0					// 1ppv4: Colourable Quarterstaves without 1ppv4: Wizards' Staves (core)
	OUTER_SET is_1pp_401    =  (MOD_IS_INSTALLED ~1pp.tp2~ ~401~) ? 1 : 0											// 1ppv4: Improved projectile effects

	OUTER_SET is_tobex = (FILE_EXISTS ~tobex_ini/tobexcore.ini~) ? 1 : 0											// ToBEx (if TobEx Magical weapon dispel behaviour option is installed)


	/* --------------------------------------------- *
	 *    General variables, macros and functions    *
	 * --------------------------------------------- */
	ACTION_IF (is_ee) BEGIN
		OUTER_SPRINT ee_file "_ee"
	END ELSE BEGIN
		OUTER_SPRINT ee_file ""
	END

	INCLUDE ~%MOD_FOLDER%/lib/gw_functions.tpa~						// Loads macros and functions library dealing with tooltip appending and specific EE items descriptions.
	ACTION_IF (is_ee OR is_1pp_staff OR is_1pp_staff0 OR is_1pp_helmet) BEGIN
		INCLUDE ~%MOD_FOLDER%/lib/1pp_macros.tpa~					// Loads 1PP macros dealing with items coloring.
		LAM ~Locations~												// DEFINES location field values needed to alter items' colors.
		INCLUDE ~%MOD_FOLDER%/lib/1pp_compatibility.tph~			// Loads functions restoring items coloration for EE games or if 1PP relevant components are installed.
	END

/*	PRINT "CONTROL VARIABLES for DEBUGGING:
is_ee = %is_ee% - is_tob = %is_tob% - is_bg2ee = %is_bg2ee% - is_eet = %is_eet% - ee_file = %ee_file%
is_1pp_helmet = %is_1pp_helmet% - is_1pp_staff = %is_1pp_staff% - is_1pp_staff0 = %is_1pp_staff0% - is_1pp_401 = %is_1pp_401% - is_tobex = %is_tobex%"*/


SILENT
	/* ---------------------------------------------------- *
	 *    Convert strings to UTF-8 for BGEE/BG2EE           *
	 *    No exception, everything needs to be converted    *
	 * ---------------------------------------------------- */
	ACTION_DEFINE_ARRAY sotm_noconvert BEGIN END
	// any tra files used in LANGUAGE need to be reloaded after UTF-8 conversion
	ACTION_DEFINE_ARRAY sotm_reload BEGIN setup strings END
	LAF HANDLE_CHARSETS
		INT_VAR
			infer_charsets = 1
		STR_VAR
			tra_path = EVAL ~%MOD_FOLDER%/lang~
			noconvert_array = sotm_noconvert
			reload_array = sotm_reload
	END

END	// of ACTION_IF !VARIABLE_IS_SET always_once_section BEGIN
